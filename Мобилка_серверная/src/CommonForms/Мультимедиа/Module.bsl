
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Качество = 100;
	#Если МобильноеПриложениеКлиент Тогда
		Камера = ТипКамерыУстройства.Авто;
		КамераПриИзменении();   
		
		КачествоВидео = КачествоВидеозаписи.Авто;
	#КонецЕсли
	ЭтоАндроид = ОбщиеФункции.ПолучитьОС() = ПредопределенноеЗначение("Перечисление.МобильныеОС.Android");	
	Элементы.ФункцииАндроид.Видимость = ЭтоАндроид;
	//Элементы.РазрешениеФото.Видимость = ЭтоАндроид;
	//Элементы.ПутьДляСохраненияФайлов.Видимость = ЭтоАндроид;
	Элементы.ДанныеВУстройстве.КоманднаяПанель.Видимость = ЭтоАндроид;
	ОткрыватьРезультат = Истина;
	АндКачествоВидео = "0";
КонецПроцедуры

#Область Функции1С
&НаКлиенте
Процедура ОбработкаКоманды(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Операция = Команда.Имя;
		Результат = Неопределено; 
		ТипДанных = Неопределено;
		Если Операция = "СделатьФото" Тогда 
			Если СредстваМультимедиа.ПоддерживаетсяФотоснимок(Камера) Тогда
				Если НЕ ЗначениеЗаполнено(Строка(РазрешениеФото)) И Элементы.РазрешениеФото.СписокВыбора.Количество() > 0 Тогда 
					РазрешениеФото = Элементы.РазрешениеФото.СписокВыбора[0].Значение;
					Сообщить("Укажите разрешение фотографии!");
					Возврат;
				КонецЕсли;
				
				Результат = СредстваМультимедиа.СделатьФотоснимок(Камера, РазрешениеФото, Качество, Монохромное);
				ТипДанных = ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Фото");
			Иначе
				Сообщить("Не поддерживается фото!");
			КонецЕсли; 
		ИначеЕсли Операция = "СделатьВидео" Тогда
			Если СредстваМультимедиа.ПоддерживаетсяВидеозапись(Камера) Тогда
				Результат = СредстваМультимедиа.СделатьВидеозапись(Камера,КачествоВидео);
				ТипДанных = ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Видео");
			Иначе
				Сообщить("Не поддерживается видео!");
			КонецЕсли; 
		ИначеЕсли Операция = "СделатьАудио" Тогда
			Если СредстваМультимедиа.ПоддерживаетсяАудиозапись() Тогда
				Результат = СредстваМультимедиа.СделатьАудиозапись();
				ТипДанных = ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Аудио");
			Иначе
				Сообщить("Не поддерживается аудио запись!");
			КонецЕсли; 
		КонецЕсли; 
		Если Результат = Неопределено Тогда 
			Сообщить("Отказ от действия!");
			Возврат;
		КонецЕсли;
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Данные", Результат.ПолучитьДвоичныеДанные());
		СтруктураДанных.Вставить("РасширениеФайла", Результат.РасширениеФайла);
		СтруктураДанных.Вставить("ТипДанных", ТипДанных);	
		
		ОткрытьРезультатМультимедиа(СтруктураДанных);
		
	#КонецЕсли
КонецПроцедуры
#КонецОбласти

#Область ФункцииАндроид
&НаКлиенте
Процедура СделатьФотоАндроид(Команда)
	
	Ответ = Вопрос("Делать серию снимков?", РежимДиалогаВопрос.ДаНетОтмена);
	Если Ответ = КодВозвратаДиалога.Отмена Тогда Возврат КонецЕсли;
	
	ЭтоСерияСнимков = Ответ = КодВозвратаДиалога.Да;
	Файлы = Новый Массив;
	#Если МобильноеПриложениеКлиент Тогда
		//Тут указываем путь, при чем путь должен быть доступен всем программам, так что временные файлы 1С не подйут
		ФайлКартинки = ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов, 1) + "FotoTestFrom1C.png";
		Если НЕ ЭтоСерияСнимков Тогда
			НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.media.action.IMAGE_CAPTURE");
			//Обязательно указываем этот параметр, если его не указать, тогда вам вернется привью файла в низком качестве и находится он будет в параметрах с ключем data.
			НовВз.ДополнительныеДанные.Добавить("output",ФайлКартинки,"Uri");
			
			Файлы.Добавить(ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов, 0) + "FotoTestFrom1C.png");
		Иначе 
			НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.media.action.STILL_IMAGE_CAMERA");
			
			//Срез до старта фото
			ФайлыДоСрез = НайтиФайлы("\sdcard\DCIM\Camera","*",Истина);
			Для Каждого Файл Из ФайлыДоСрез Цикл
				Файлы.Добавить(Файл.ПолноеИмя);
			КонецЦикла;
		КонецЕсли;
		
		//Если фото не сделано, то ответ будет "0"
		Если НовВз.Запустить(Истина) Тогда
			//Все удалось
		Иначе
			Сообщить("Фото не сделано!");
			Возврат;
		КонецЕсли;     
		
		//Поиск новых файлов нужен только при серии снимков
		ФайлыСписок = ПолучитьФайлыСерииФото(Файлы, ЭтоСерияСнимков);
		Для Каждого СтрФайла Из ФайлыСписок Цикл
			ОткрытьРезультатМультимедиа(СтрФайла);
		КонецЦикла;
		
	#КонецЕсли
КонецПроцедуры

Функция ПолучитьФайлыСерииФото(Файлы,ЭтоСерияСнимков)
	//Сред после окончания фотографирования
	Если ЭтоСерияСнимков Тогда
		ФайлыПосле = НайтиФайлы("\sdcard\DCIM\Camera","*",Истина);
		Если Файлы.Количество() >= ФайлыПосле.Количество() Тогда
			Возврат Неопределено
		КонецЕсли;
		
	Иначе
		ФайлыПосле = Новый Массив;
	КонецЕсли;
	
	Для Каждого Файл Из ФайлыПосле Цикл
		Файлы.Добавить(Файл.ПолноеИмя);
	КонецЦикла;
	
	//Создаем ТЗ для дальнейшей свертки данных
	Таб = Новый ТаблицаЗначений;
	Таб.Колонки.Добавить("Данные");
	Таб.Колонки.Добавить("Вхождения");
	Для Н=1 По Файлы.Количество() Цикл
		Таб.Добавить();
	КонецЦикла; 

	Таб.ЗагрузитьКолонку(Файлы,"Данные");
	Таб.ЗаполнитьЗначения(1, "Вхождения");
	Таб.Свернуть("Данные", "Вхождения");
	ОтобранныеДанные = Таб.НайтиСтроки(Новый Структура("Вхождения", 1));
	//Помещаем данные в масив для возврата на верхний уровень
	
	МассивФайлов = Новый Массив;
	Для Каждого Стр Из ОтобранныеДанные Цикл
		Файл = Новый Файл(Стр.Данные);
		Если НЕ Файл.Существует() Тогда
			Сообщить("Ошибка! Файл не найден! " + Стр.Данные);
		КонецЕсли;
		Если Файл.ЭтоКаталог() Тогда Продолжить КонецЕсли;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Данные", Новый ДвоичныеДанные(Стр.Данные));
		СтруктураДанных.Вставить("РасширениеФайла", Файл.Расширение);
		
		СтруктураДанных.Вставить("ТипДанных", ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Фото"));
		МассивФайлов.Добавить(СтруктураДанных);
	КонецЦикла;
	Возврат МассивФайлов
КонецФункции

&НаКлиенте                  
Процедура ОбрезатьФото(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ФайлКартинки = "file://" + ОбщиеФункции.ОбработкаВызоваВыбораПути(,"image/*");
	    ФайлВр = ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов, 1) + "FotoTestFrom1C.png";
		НовВз = Новый ЗапускПриложенияМобильногоУстройства("com.android.camera.action.CROP", ФайлКартинки);        
		НовВз.Тип = "image/*";
		НовВз.ДополнительныеДанные.Добавить("aspectX", 1);
		НовВз.ДополнительныеДанные.Добавить("aspectY", 1);
		
		НовВз.ДополнительныеДанные.Добавить("outputX", 50);
		НовВз.ДополнительныеДанные.Добавить("outputY", 50);

		//НовВз.ДополнительныеДанные.Добавить("output", ФайлВр);
		НовВз.ДополнительныеДанные.Добавить("crop", true);
		НовВз.ДополнительныеДанные.Добавить("return-data", true);
		НовВз.ДополнительныеДанные.Добавить("outputFormat", "png");
		РезультатРаботы = НовВз.Запустить(Истина);
		Если НЕ РезультатРаботы Тогда
			Сообщить("Фото не обработано!");
			Возврат
		КонецЕсли;
		
		//ДД = Новый ДвоичныеДанные(ПолучитьПуть(0) + "FotoTestFrom1C.png");
		ДанныеРезультат = НовВз.ДополнительныеДанные.Получить("data");
		Если НЕ ЗначениеЗаполнено(ДанныеРезультат) Тогда Возврат КонецЕсли;
		ДД = ДанныеРезультат.Значение.ПолучитьДвоичныеДанные(); //Получаем двочные данные
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Данные", ДД);
		СтруктураДанных.Вставить("РасширениеФайла", "png");
		СтруктураДанных.Вставить("ТипДанных", ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Фото"));
		//Получим превью фото, если мы не указали параметр - куда сохранять фото.
		//СтруктураДанных.Вставить("Данные", НовВз.ДополнительныеДанные.Получить("data").Значение);
		ОткрытьРезультатМультимедиа(СтруктураДанных);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьВидеоАндроид(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		//Тут указываем путь, при чем путь должен быть доступен всем программам, так что временные файлы 1С не подйут
		ФайлВидео = ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов,1) + "VideoTestFrom1C.mp4";
		НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.media.action.VIDEO_CAPTURE");
		//НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.media.action.VIDEO_CAMERA");
		//Обязательно указываем этот параметр, если его не указать, тогда вам вернется привью файла в низком качестве и находится он будет в параметрах с ключем data.
		НовВз.ДополнительныеДанные.Добавить("output",ФайлВидео,"Uri");
		
		НовВз.ДополнительныеДанные.Добавить("android.intent.extra.videoQuality",Число(АндКачествоВидео));
		Если АндДлительностьВидео Тогда
			НовВз.ДополнительныеДанные.Добавить("android.intent.extra.durationLimit",АндДлительностьВидео, "int");
		КонецЕсли;
		//Если видео не сделано, то ответ будет "0"
		Если НовВз.Запустить(Истина) Тогда
			//ну а тут уже готовое видео, так что все что хотим, то и делаем.
		Иначе
			Сообщить("Видео не сделано!");
			Возврат;
		КонецЕсли;
		
		Файл = Новый Файл(ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов,0) + "VideoTestFrom1C.mp4");
		Если НЕ Файл.Существует() Тогда
			Сообщить("Ошибка! Файл не найден! " + ОбщиеФункции.ПолучитьПуть(ПутьДляСохраненияФайлов, 0) + "VideoTestFrom1C.mp4");
		КонецЕсли;

		
		ДД = Новый ДвоичныеДанные(Файл.ПолноеИмя);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("Данные", ДД);
		СтруктураДанных.Вставить("РасширениеФайла", "mp4");	
		СтруктураДанных.Вставить("ТипДанных", ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Видео"));
		ОткрытьРезультатМультимедиа(СтруктураДанных);
		
	#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область ДопФункции

&НаКлиенте
Процедура КамераПриИзменении()
	#Если МобильноеПриложениеКлиент Тогда
		РазрешенияКамеры = СредстваМультимедиа.ПолучитьПоддерживаемыеРазрешенияКамеры(Камера);
		Если НЕ РазрешенияКамеры = Неопределено Тогда
			Элементы.РазрешениеФото.СписокВыбора.ЗагрузитьЗначения(РазрешенияКамеры);	
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПутьДляСохраненияФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПутьДляСохраненияФайлов = ОбщиеФункции.ОбработкаВызоваВыбораПути(Истина, "folder/*");	
КонецПроцедуры



&НаКлиенте
Процедура ОткрытьРезультатМультимедиа(СтрМультимедиа)

	Если ОткрыватьРезультат Тогда
		Если Вопрос("Вы хотите просмотреть результат?",РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда Возврат КонецЕсли;
		ОткрытьФорму("РегистрСведений.ДанныеМультимедиа.ФормаЗаписи",Новый Структура("СтрДанных", СтрМультимедиа));
	Иначе //иначе запишем данные в базу
		ЗаписатьДанныеВРегистр(СтрМультимедиа);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеВРегистр(Знач СтрМультимедиа)
	UID = Новый УникальныйИдентификатор;
	Рег = РегистрыСведений.ДанныеМультимедиа.СоздатьНаборЗаписей();
	
	Рег.Отбор.UID.Установить(UID);
	
	Нов = Рег.Добавить();
	Нов.UID = UID;
	Нов.Данные = Новый ХранилищеЗначения(СтрМультимедиа.Данные);
	Нов.Период = ТекущаяДата();
	Нов.РазмерДанных = СтрМультимедиа.Данные.Размер();
	Нов.ТипДанных = СтрМультимедиа.ТипДанных;
	Нов.РасширениеФайла = СтрМультимедиа.РасширениеФайла;
	Рег.Записать(Истина);
КонецПроцедуры

&НаСервере
Функция ПолучитьMIMEТип(Знач ТипДанных)
	
	Возврат Метаданные.Перечисления.ТипХранимыхДанных.ЗначенияПеречисления[Строка(ТипДанных)].Комментарий;
	
КонецФункции

&НаКлиенте
Процедура КачествоПриИзменении(Элемент)
	Элементы.Качество.Заголовок = "Качество " + Качество + "%";
КонецПроцедуры

#КонецОбласти
