
&НаКлиенте
Процедура СписокПровайдеров(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		
		Провайдеры = СредстваГеопозиционирования.ПолучитьПровайдеров();
		Для Каждого Провайдер Из Провайдеры Цикл
			Сообщить(Провайдер.Имя);
		КонецЦикла;
		СамыйТочный = СредстваГеопозиционирования.ПолучитьСамогоТочногоПровайдера();
		Если ЗначениеЗаполнено(СамыйТочный) Тогда
			Сообщить("Самый точный:" + СамыйТочный.Имя);
		Иначе
			Сообщить("Не найден точный провайдер!");
		КонецЕсли;
		СамыйЭкономный = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
		Если ЗначениеЗаполнено(СамыйЭкономный) Тогда
			Сообщить("Самый экономичный:" + СамыйЭкономный.Имя);
		Иначе
			Сообщить("Не найден экономный провайдер!");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОпции(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		
		МассивСвойств = Новый Массив;
		МассивСвойств.Добавить("Имя");
		МассивСвойств.Добавить("ИспользуетСетьПередачиДанных");
		МассивСвойств.Добавить("ИспользуетСотовуюСеть");
		МассивСвойств.Добавить("ИспользуетСпутники");
		МассивСвойств.Добавить("Платный");
		МассивСвойств.Добавить("ПоддерживаетВысоту");
		МассивСвойств.Добавить("ПоддерживаетНаправление");
		МассивСвойств.Добавить("ПоддерживаетСкорость");
		МассивСвойств.Добавить("Точность");
		МассивСвойств.Добавить("Энергопотребление");
		
		Провайдеры = СредстваГеопозиционирования.ПолучитьПровайдеров();
		Для каждого Провайдер Из Провайдеры Цикл
			Стр = "";
			Для каждого СвойствоПровайдера Из МассивСвойств Цикл
				Стр = Стр + СвойствоПровайдера + ": " + Провайдер[СвойствоПровайдера] + Символы.ПС
			КонецЦикла;       
			Сообщить(Стр);
		КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекущиеКоординаты(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		
		МассивСвойств = Новый Массив;
		МассивСвойств.Добавить("ВертикальнаяТочность");
		МассивСвойств.Добавить("ГоризонтальнаяТочность");
		МассивСвойств.Добавить("Дата");
		МассивСвойств.Добавить("Координаты");
		МассивСвойств.Добавить("Направление");
		МассивСвойств.Добавить("Скорость");
		
		Провайдеры = СредстваГеопозиционирования.ПолучитьПровайдеров();
		Для каждого Провайдер Из Провайдеры Цикл
			Сообщить("Данные провайдера " + Провайдер.Имя);
			Данные = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(Провайдер.Имя); 
			Если НЕ ЗначениеЗаполнено(Данные) Тогда
				Сообщить("Не удалось получить координаты!");
				Возврат;
			КонецЕсли;
			Для каждого СтрДан Из МассивСвойств Цикл
				Сообщить(СтрДан + ": " + Данные[СтрДан])      
			КонецЦикла; 
		КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНаКартеКоординаты(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		
		Провайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
		Данные = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(Провайдер.Имя);
		Если НЕ ЗначениеЗаполнено(Данные) Тогда
			Сообщить("Не удалось получить координаты!");
			Возврат;
		КонецЕсли;
		ПоказатьНаКарте(Данные.Координаты);
		ЗаписатьПуть(Данные);
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.Координаты"));
	#КонецЕсли
	
КонецПроцедуры

Процедура ЗаписатьПуть(Данные)
	Рег = РегистрыСведений.Координаты.СоздатьМенеджерЗаписи();
	Рег.Период = Данные.Дата;
	Рег.Долгота = Данные.Координаты.Долгота;
	Рег.Широта = Данные.Координаты.Широта;
	Попытка
		Рег.Записать(Ложь);
	Исключение
		Сообщить("Координаты не обновлены!")
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМестоположение(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Провайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
		
		Если СредстваГеопозиционирования.ОбновитьМестоположение(Провайдер.Имя,"10") Тогда
			Сообщить("Данные получены!");
		иначе
			Сообщить("Ошибка обновления данных!");
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры


&НаКлиенте
Процедура СформироватьСписокКоординат(Команда)
	#Если МобильноеПриложениеКлиент Тогда

	ПоказатьНаКарте(СформироватьСписокКоординатНаСервере());
	#КонецЕсли

КонецПроцедуры


&НаСервере
Функция СформироватьСписокКоординатНаСервере()
	
	Список = Новый СписокЗначений;
	ВыборкаРегистра = РегистрыСведений.Координаты.Выбрать();
	Пока ВыборкаРегистра.Следующий() Цикл
		Список.Добавить(Новый ГеографическиеКоординаты(ВыборкаРегистра.Широта, ВыборкаРегистра.Долгота),ВыборкаРегистра.Период);
	КонецЦикла; 
	Возврат Список;
	
КонецФункции


&НаКлиенте
Процедура ВключитьАвтоЗаписьКоординат(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Провайдер = СредстваГеопозиционирования.ПолучитьСамогоТочногоПровайдера();
		Оп = Новый ОписаниеОповещения("ОбработкаОповещенияПровайдера",ЭтаФорма, "Некая строка");
		СредстваГеопозиционирования.ПодключитьОбработчикИзмененияМестоположения(Оп, Провайдер.Имя, 100,10);
		Элементы.ВключитьАвтоЗаписьКоординат.Доступность = Ложь;
		Элементы.ОтключитьАвтоЗаписьКоординат.Доступность = Истина;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияПровайдера(ИмяПровайдера,ДанныеМестоположения, ДопПараметр) Экспорт
	ЗаписатьПуть(ДанныеМестоположения);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.Координаты"));
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьАвтоЗаписьКоординат(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Провайдер = СредстваГеопозиционирования.ПолучитьСамогоТочногоПровайдера();
		СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения();
		Элементы.ВключитьАвтоЗаписьКоординат.Доступность = Истина;
		Элементы.ОтключитьАвтоЗаписьКоординат.Доступность = Ложь;
	#КонецЕсли
КонецПроцедуры


&НаКлиенте
Процедура ОтобразитьКоординаты(Команда)
	
	
	ЗапуститьПриложение(СформироватьСписокКоординатНаСервереДляКарт());
КонецПроцедуры

&НаСервере
Функция СформироватьСписокКоординатНаСервереДляКарт()
	УРЛ = "http://www.google.com.ua/maps/dir";
	ВыборкаРегистра = РегистрыСведений.Координаты.Выбрать();
	Индекс = 0;
	Пока ВыборкаРегистра.Следующий() Цикл
		Индекс = Индекс + 1;
		УРЛ = УРЛ + "/" +  Формат(ВыборкаРегистра.Широта,"ЧРД=.; ЧГ=0") + "," + Формат(ВыборкаРегистра.Долгота,"ЧРД=.; ЧГ=0");
		//Если Индекс = 2 Тогда прервать КонецЕсли;
	КонецЦикла; 
	Возврат УРЛ;
	
КонецФункции

&НаКлиенте
Процедура ОчисткаПройденногоПути(Команда)
	ОчисткаПройденногоПутиНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчисткаПройденногоПутиНаСервере()
	Рег = РегистрыСведений.Координаты.СоздатьНаборЗаписей();
	Рег.Записать(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПутьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	#Если МобильноеПриложениеКлиент Тогда
	СтандартнаяОбработка = ложь;
	Адрес = ПолучитьАдресПоМестоположению(Новый ГеографическиеКоординаты(Элементы.Путь.ТекущиеДанные.Широта,Элементы.Путь.ТекущиеДанные.Долгота));
	Сообщить(?(Адрес = Неопределено, "Не удалось получить адрес", Адрес.Представление));
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКоординатыПоАдресу(Команда)
	#Если МобильноеПриложениеКлиент Тогда
	Структура = Новый Структура("Страна,Регион,Город,Улица,Дом,Индекс,Представление","Россия","","Санкт-Петербург г","Добровольцев ул","1","190000","190000, Санкт-Петербург г, Москва, Добровольцев ул, дом № 1");
	Адрес = Новый ДанныеАдреса(Структура);
	Координаты = ПолучитьМестоположениеПоАдресу(Адрес);
	Если Координаты = Неопределено Тогда
		Сообщить("Не удалось получить координаты");
	Иначе
		ПоказатьНаКарте(Координаты);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьАдресПоКоординатам(Команда)
	#Если МобильноеПриложениеКлиент Тогда
	Стр = Элементы.Путь.ТекущиеДанные;
	Адрес = ПолучитьАдресПоМестоположению(Новый ГеографическиеКоординаты(Стр.Широта, Стр.Долгота));
	Сообщить(?(Адрес = Неопределено, "Не удалось получить адрес", Адрес.Представление));
	#КонецЕсли
КонецПроцедуры



