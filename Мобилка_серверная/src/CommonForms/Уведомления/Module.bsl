
&НаКлиенте
Процедура ЛокальноеУведомление(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Уведомление = СоздатьУведомление();
		ДоставляемыеУведомления.ДобавитьЛокальноеУведомление(Уведомление);
		Сообщить("Локальное уведомление установлено!");
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Функция СоздатьУведомление()
	Уведомление = Новый ДоставляемоеУведомление;
	Уведомление.Заголовок = ЗаголовокСообщения;
	Уведомление.Текст = Текст;
	Уведомление.Данные = Данные;
	Уведомление.ДатаПоявленияУниверсальноеВремя = ?(ДатаПоявления = Дата(1,1,1), УниверсальноеВремя(ТекущаяДата()) + 10, ДатаПоявления);;
	Уведомление.ИнтервалПовтора = ИнтервалПовтора;
	Уведомление.ЗвуковоеОповещение = ЗвуковоеОповещение.ПоУмолчанию;
	Уведомление.Наклейка = Наклейка;
	Возврат Уведомление;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Текст = "Текст оповещения";
	Наклейка = 1;
	ИнтервалПовтора = 0;
	ЗаголовокСообщения = "Заголовок оповещения";
	Данные = "Некие данные";
	/////////////////////////////
	IDПроекта = "896392358441";
	КлючСервера	= "AIzaSyCkA5qHGw_TuhsuKvb8UIAVcROYEWoiQQU";
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаклейку(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ДоставляемыеУведомления.УстановитьНаклейку(Наклейка);
		Сообщить("Наклейка установлена. Вернитесь на домашний экран.");
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТекущуюНаклейку(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ТекНаклейка = ДоставляемыеУведомления.ПолучитьНаклейку();
		Сообщить("Текущая наклейка: " + ТекНаклейка);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсеУведомления(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ДоставляемыеУведомления.ОтменитьЛокальныеУведомления();
		Сообщить("Все уведомления были отменены. В том числе и периодические!");
	#КонецЕсли
КонецПроцедуры


&НаКлиенте
Процедура ПодключитьОбработчикУведомлений(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Оп = Новый ОписаниеОповещения("ОбработчикЛокальныхУведомлений", ОбщиеФункции, "Этот обработчик вызван из формы");
		ДоставляемыеУведомления.ПодключитьОбработчикУведомлений(Оп);
	#КонецЕсли
КонецПроцедуры


&НаКлиенте
Процедура ОтключитьОбработчикУведомлений(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		Оп = Новый ОписаниеОповещения("ОбработчикЛокальныхУведомлений", ОбщиеФункции);
		ДоставляемыеУведомления.ОтключитьОбработчикУведомлений(Оп);
	#КонецЕсли
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////
//Получаем ID на мобильном клиенте, сериализуем его в строку
&НаКлиенте
Процедура ПолучитьIDУстройства(Команда)
	#Если МобильноеПриложениеКлиент Тогда
		ID = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений(IDПроекта);	
		IDУстройства = СервисныйМодуль.Сериализовать(ID);
	#КонецЕсли
КонецПроцедуры
//На стационарном компьютере - десериализуем, и отправляем сообщение
&НаКлиенте
Процедура ОтправитьГлобальноеУведомление(Команда)
	#Если ТонкийКлиент ИЛИ ТолстыйКлиент Тогда
		ID = СервисныйМодуль.Десериализовать(IDУстройства);
		Уведомление = СоздатьУведомление();
		
		Уведомление.Получатели.Добавить(ID);
		ОтправкаДоставляемыхУведомлений.Отправить(Уведомление,КлючСервера);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЧерезAPI(Команда)
	ID = СервисныйМодуль.Десериализовать(IDУстройства);
	Уведомление = СоздатьУведомление();

	ОтправитьСообщениеНаДругоеУстройство(Уведомление,ID)
КонецПроцедуры

#Область ОповещениеДругихУстройств
&НаКлиенте
Процедура ОтправитьСообщениеНаДругоеУстройство(Уведомление,ID)
 	

	СертификатОпр = Новый ЗащищенноеСоединениеOpenSSL(,);
	ХТТП = Новый HTTPСоединение("android.googleapis.com",443,,,,, СертификатОпр);
	
	СтруктураЗапроса = Новый Соответствие;
	СтруктураЗапроса.Вставить("Content-Type", "application/x-www-form-urlencoded;charset=UTF-8");
	СтруктураЗапроса.Вставить("Authorization", "key=" + КлючСервера);
	ХТТПЗапрос = Новый HTTPЗапрос("gcm/send",СтруктураЗапроса);
	Тело = "registration_id=" + ID.ИдентификаторУстройства + "
		|&data.data=" + СервисныйМодуль.URLEncode(Уведомление.Данные) + "&data.text=" + СервисныйМодуль.URLEncode(Уведомление.Текст) + "&data.base="+ID.ИдентификаторИнформационнойБазы ;
	ХТТПЗапрос.УстановитьТелоИзСтроки(Тело,"UTF-8");
	Ответ = ХТТП.ОтправитьДляОбработки(ХТТПЗапрос);
КонецПроцедуры
#КонецОбласти

