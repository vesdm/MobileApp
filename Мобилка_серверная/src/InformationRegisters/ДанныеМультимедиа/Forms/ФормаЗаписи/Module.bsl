&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.СтрДанных) Тогда
		ДанныеВСтр = Параметры.СтрДанных;
		Запись.Период = ТекущаяДата();
		Запись.UID = Новый УникальныйИдентификатор;
		Запись.ТипДанных = ДанныеВСтр.ТипДанных;
		Запись.РазмерДанных = ДанныеВСтр.Данные.Размер();
		Запись.РасширениеФайла = ДанныеВСтр.РасширениеФайла; 
		
		ДДФайла = ДанныеВСтр.Данные;
		ЭтаФорма.Модифицированность = Истина;
	Иначе
		ПолучитьДанныеФото();
	КонецЕсли;                                                       
	
	Если  Запись.ТипДанных = Перечисления.ТипХранимыхДанных.Фото Тогда
		//Элементы.Картинка.Видимость = Истина;
		Картинка = ПоместитьВоВременноеХранилище(ДДФайла);
		Элементы.Картинка.РазмерКартинки = РазмерКартинки.Пропорционально;

	ИначеЕсли Запись.ТипДанных.Пустая() Тогда
		Элементы.ТипДанных.ТолькоПросмотр = Ложь;
		ЭтоРучнаяЗагрузка = Истина;
		Элементы.Картинка.ТекстНевыбраннойКартинки = "Укажите тип загружаемых данных слева вверху." + Символы.ПС + 
		"После этого нажмите на ЭТО поле, что бы выбрать файл, который вы хотите загрузить в базу.";
	Иначе		
		Элементы.Картинка.ТекстНевыбраннойКартинки = "Нажмите для воспроизведения " + Запись.ТипДанных;
	КонецЕсли;
	
	Размер = ПолучитьРазмер(Запись.РазмерДанных);
	
КонецПроцедуры

Процедура ПолучитьДанныеФото()
	Рег = РегистрыСведений.ДанныеМультимедиа.СоздатьНаборЗаписей();
	Рег.Отбор.UID.Установить(Запись.UID);
	Рег.Отбор.Период.Установить(Запись.Период);
	Рег.Прочитать();
	Если Рег.Количество() = 0 Тогда Возврат КонецЕсли;
	ДДФайла = Рег[0].Данные.Получить();
	
КонецПроцедуры

Функция ПолучитьРазмер(Размер)
	Если Размер < 1024 Тогда
		Возврат ""+Размер + "Б";
	ИначеЕсли Размер < 1024*1024 Тогда 
		Возврат "" + Цел(Размер/1024) + "кБ";
	Иначе
		Возврат "" + Цел(Размер/(1024*1024)) + "мБ";
	КонецЕсли; 
КонецФункции

&НаКлиенте
Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;
	Если Это_iOS Тогда 
		Ответ = "Открыть" 
	Иначе
		СписокЗн = Новый СписокЗначений;
		СписокЗн.Добавить("Отмена", "Отмена");
		Если ЭтоРучнаяЗагрузка Тогда
			СписокЗн.Добавить("Выбрать", "Выбрать файл");
		Иначе
			Если Запись.ТипДанных = ПредопределенноеЗначение("Перечисление.ТипХранимыхДанных.Фото") Тогда	
				СписокЗн.Добавить("Обрезать", "Обрезать");
				СписокЗн.Добавить("Редактировать", "Редактировать");
			КонецЕсли;
			СписокЗн.Добавить("Отправить", "Отправить");
			СписокЗн.Добавить("Открыть", "Открыть");
		КонецЕсли;
		Ответ = Вопрос("Что Вы хотите сделать?", СписокЗн);
	КонецЕсли;
	Если Ответ = "Открыть" Тогда
		Путь = КаталогДокументов() + Новый УникальныйИдентификатор() + Запись.РасширениеФайла;
		ДДФайла.Записать(Путь);
		Если Это_iOS Тогда
			ЗапуститьПриложение(Путь);
		Иначе
			
			#Если МобильноеПриложениеКлиент Тогда
				
				НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.VIEW","file://" + Путь);
				НовВз.Тип = ПолучитьMIMEТип(Запись.ТипДанных);
				НовВз.Запустить(Истина);
				//Нас не интересует сам результат, а нас интересует факт того, что просмотр данных окончился
				//теперь можем их удалить
				УдалитьФайлы(Путь);
			#КонецЕсли
			
		КонецЕсли;
	ИначеЕсли Ответ = "Отправить" Тогда
		Путь = КаталогДокументов() + Новый УникальныйИдентификатор() + "." + Запись.РасширениеФайла;
		ДДФайла.Записать(Путь);					

		#Если МобильноеПриложениеКлиент Тогда
			
			НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.SENDTO");
			НовВз.Тип = ПолучитьMIMEТип(Запись.ТипДанных);
			НовВз.ДополнительныеДанные.Добавить("android.intent.extra.STREAM","file://" + Путь,"Uri");
			НовВз.ДополнительныеДанные.Добавить("android.intent.extra.SUBJECT","Отправка с мобильной 1С");
			НовВз.ДополнительныеДанные.Добавить("android.intent.extra.TEXT",Запись.Комментарий);
			
			НовВз.Запустить(Истина);
			//Нас не интересует сам результат, а нас интересует факт того, что просмотр данных окончился
			//теперь можем их удалить
			УдалитьФайлы(Путь);
		#КонецЕсли

	ИначеЕсли Ответ = "Обрезать" Тогда
		Путь = КаталогДокументов() + Новый УникальныйИдентификатор() + Запись.РасширениеФайла;
		ДДФайла.Записать(Путь);
		
		#Если МобильноеПриложениеКлиент Тогда
			
			НовВз = Новый ЗапускПриложенияМобильногоУстройства("com.android.camera.action.CROP", "file://" + Путь);        
			НовВз.Тип = "image/*";
			//НовВз.ДополнительныеДанные.Добавить("output", ФайлВр);
			НовВз.ДополнительныеДанные.Добавить("crop", true);
			НовВз.ДополнительныеДанные.Добавить("return-data", true);
			НовВз.ДополнительныеДанные.Добавить("outputFormat", "png");
			РезультатРаботы = НовВз.Запустить(Истина);
			
			
			УдалитьФайлы(Путь);
			Если НЕ РезультатРаботы Тогда
				Сообщить("Фото не обработано!");
				Возврат
			КонецЕсли;
			Картинка = Неопределено;
			ДДФайла = НовВз.ДополнительныеДанные.Получить("data").Значение.ПолучитьДвоичныеДанные(); //Двоичные данные
			Запись.РазмерДанных = ДДФайла.Размер();
			Размер = ПолучитьРазмер(Запись.РазмерДанных);
			КартинкаНажатиеФрагмент();
		#КонецЕсли
		ИначеЕсли Ответ = "Редактировать" Тогда 
		Путь = КаталогДокументов() + Новый УникальныйИдентификатор() + Запись.РасширениеФайла;
		ДДФайла.Записать(Путь);
		
		#Если МобильноеПриложениеКлиент Тогда
			
			НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.EDIT", "file://" + Путь);        
			НовВз.Тип = ПолучитьMIMEТип(Запись.ТипДанных);
			РезультатРаботы = НовВз.Запустить(Истина);
			Если НЕ РезультатРаботы Тогда
				Сообщить("Не удалось запустить программу редактирования!");
				Возврат
			КонецЕсли;
			//ТекущаяДата5= ТекущаяДата() + 5;
			//Пока ТекущаяДата() < ТекущаяДата5 Цикл КонецЦикла;
			
			Картинка = Неопределено;
			//ДДФайла = НовВз.ДополнительныеДанные.Получить("data").Значение.ПолучитьДвоичныеДанные(); //Двоичные данные
			ДДФайла = Новый ДвоичныеДанные(Путь);
			УдалитьФайлы(Путь);
			Запись.РазмерДанных = ДДФайла.Размер();
			Размер = ПолучитьРазмер(Запись.РазмерДанных);
			КартинкаНажатиеФрагмент();
		#КонецЕсли

	ИначеЕсли Ответ = "Выбрать" Тогда
		Путь = ОбщиеФункции.ОбработкаВызоваВыбораПути(,ПолучитьMIMEТип(Запись.ТипДанных), Это_iOS);
		Файл = Новый Файл("file://" + Путь);
		ДДФайла = Новый ДвоичныеДанные(Путь);
		Запись.РасширениеФайла = Файл.Расширение;
		Запись.Комментарий = "Загружено с устройства. " + Файл.Имя;
		Запись.РазмерДанных = ДДФайла.Размер();
		Размер = ПолучитьРазмер(Запись.РазмерДанных);
		КартинкаНажатиеФрагмент();
		Элементы.ТипДанных.ТолькоПросмотр = Истина;
		ЭтоРучнаяЗагрузка = Ложь; //Когда выбрали файл, считаем что это уже созданая запись
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КартинкаНажатиеФрагмент()
	
	Картинка = ПоместитьВоВременноеХранилище(ДДФайла);
		
КонецПроцедуры

&НаСервере
Функция ПолучитьMIMEТип(Знач ТипДанных)
	
	Возврат Метаданные.Перечисления.ТипХранимыхДанных.ЗначенияПеречисления[Строка(ТипДанных)].Комментарий;
	
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Данные = Новый ХранилищеЗначения(ДДФайла);
КонецПроцедуры
